# Maintainer: shmilee <shmilee.zju@gmail.com>

pkgname=gdpy3
pkgver=0.8.4
pkgrel=1
pkgdesc="Gyrokinetic Toroidal Code Data Processing tools written in python3"
url="https://github.com/shmilee/gdpy3.git"
arch=('any')
license=('MIT')
depends=('python-numpy' 'python-scipy' 'python-matplotlib')
optdepends=('xz: for npz lzma compression method'
            'python-h5py: saving and loading hdf5 support'
            'python-paramiko: loading raw data through ssh'
            'tk: tkinter GUI support'
            'python-screeninfo>=0.4.1: tkinter GUI auto-choose screen support'
            'jupyter-notebook: GUI in Jupyter Notebook support'
            'python-ipywidgets: GUI in Jupyter Notebook support'
            'python-pillow: display graphics in terminal support'
            'libsixel: display graphics in terminal support'
            'python-argcomplete: autocomplete in shell support')
makedepends=('python-build' 'python-installer' 'python-wheel')
source=("https://github.com/shmilee/$pkgname/archive/v$pkgver.tar.gz" "gdpy3.desktop" "gdpy3-data.xml")
sha512sums=('SKIP'
            'cfcbbfe84da78d91b0e6bcc424fa8f24b68a49f036d510e4b308e9f22b746475e72a7e4c62f752a9f7aaeb30ff5553f602e81cd03f77772a9fb3dd1ad22d09e1'
            '39c33743f7a298c0b321b3799df318adab395b038c93641ff9cc23ffae2c60ff7a74ce0cd9a4de265bd37d27a97721f8a3801152aadb214d6de4539555d794d0')

build() {
  if [ -d "$pkgname-$pkgver" ]; then
    cd $pkgname-$pkgver
  elif [ -d "${pkgname%-git}" ]; then
    cd ${pkgname%-git}
  fi
  # python setup.py build
  python -m build --wheel --no-isolation
}

package() {
  if [ -d "$pkgname-$pkgver" ]; then
    cd $pkgname-$pkgver
  elif [ -d "${pkgname%-git}" ]; then
    cd ${pkgname%-git}
  fi
  # python setup.py install --root="$pkgdir"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  pycmd="import src.__about__;  print(src.__about__.__icon_path__)"
  icon_path=$(python -c "$pycmd")
  install -Dm644 "$icon_path" "$pkgdir"/usr/share/icons/hicolor/128x128/apps/gdpy3.png
  install -Dm644 ../gdpy3.desktop "$pkgdir"/usr/share/applications/gdpy3.desktop
  install -Dm644 ../gdpy3-data.xml "$pkgdir"/usr/share/mime/packages/gdpy3-data.xml
}
